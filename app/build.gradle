plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def keystoreProperties = loadKeyStoreProperties()

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        applicationId "com.tatsuki.photom"
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
    signingConfigs {
        release {
            if(!keystoreProperties.getProperty("storeFile", "").empty){
                storeFile file(keystoreProperties.getProperty("storeFile", ""))
            }
            storePassword keystoreProperties.getProperty("storePassword", "")
            keyAlias keystoreProperties.getProperty("keyAlias", "")
            keyPassword keystoreProperties.getProperty("keyPassword", "")
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lintOptions {
        disable('AllowBackup', 'GoogleAppIndexingWarning', 'MissingApplicationIcon')
    }
}

private def loadKeyStoreProperties() {
    def keystorePropertiesFile = project.rootProject.file('keystore.properties')
    if (!keystorePropertiesFile.exists()) {
        keystorePropertiesFile.createNewFile()
    }
    def keystoreProperties = new Properties()
    keystoreProperties.load(keystorePropertiesFile.newDataInputStream())
    return keystoreProperties
}

hilt {
    enableAggregatingTask = true
}

dependencies {
    implementation project(":core")
    implementation project(":data:entity")
    implementation project(":data:extension")
    implementation project(":feature:slideshow")
    implementation project(":feature:weather")
    implementation project(":feature:devicecontrol")

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation deps.app_compat
    implementation deps.constraint_layout
    implementation deps.core_ktx
    implementation deps.kotlin.stdlib

    // Coroutine
    implementation deps.coroutines.android

    // Navigation
    implementation deps.navigation.fragment_ktx
    implementation deps.navigation.ui_ktx

    // LoopingViewPager
    implementation deps.looping_view_pager

    // Glide
    implementation deps.glide.runtime
    annotationProcessor deps.glide.compiler
    kapt deps.glide.compiler

    // Timber
    implementation deps.timber

    // Hilt
    implementation deps.hilt.android
    implementation deps.hilt_work.runtime
    kapt deps.hilt.compiler
    kapt deps.hilt_work.compiler

    testImplementation deps.junit
    androidTestImplementation deps.ext_junit
    androidTestImplementation deps.espresso
    compileOnly deps.android_things

}
